repositories {
    mavenCentral()
}

ext {
    slf4jVersion  = '1.7.30'
    logbackVersion  = '1.2.3'

    junitJupiterVersion = '5.6.3'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

jar {
    archiveBaseName = 'junit5-tagging-filtering'
    archiveVersion = '1.0.0'
}

test {
    useJUnitPlatform {
        includeTags 'functional'
        excludeTags 'non-functional'
        includeEngines 'junit-jupiter'
        excludeEngines 'junit-vintage'
    }
    
    filter {
        includeTestsMatching '.*Spec'
        includeTestsMatching '.*Test'
        includeTestsMatching '.*Tests'
        includeTestsMatching 'io.github.bonigarcia.*'
    }

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs += '-parameters'
}

dependencies {
    implementation("org.slf4j:slf4j-api:${slf4jVersion}")
    implementation("ch.qos.logback:logback-classic:${logbackVersion}")

    testImplementation("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
}
